# Functions and Delegates

MonoBridge favors a functional style, offering flexibility and multiple features for creating and using functions.

## Functions

### Classical Functions

Basic functions can be defined without parameters.

```monobridge
void Hi():
    logInfo("Hi")
```

### Functions with Parameters

Functions can accept parameters of various types.

```monobridge
void Hi(string name):
    logInfo("Hi, " + name)

Hi("world")
```

### Functions with Return Values

Functions can return values by specifying the return type.

```monobridge
number<int> Sum(number<int> a, number<int> b):
    return a + b

Sum(1, 2)
```

### Functions with Default Parameters

Parameters can have default values, allowing for more flexible calls.

```monobridge
void Hi(number b, string name = "world", number z = "Another param"):
    logInfo("Hi, " + name)
    logInfo("b = " + b)
    logInfo("z = " + z)

// Call with default parameters
Hi(10)

// Call with a custom parameter
Hi(10, "John")

// Call with a named custom parameter
Hi(10, name: "John")

// Call with named custom parameter, ignoring `name`
Hi(10, z: 20)
```

### Functions with Variable Number of Arguments

The `params` keyword allows for a variable number of arguments.

```monobridge
number Sum(params number[] numbers):
    number sum = 0
    foreach (var n in numbers):
        sum += n
    return sum
```

### Inline Parameters

Inline parameters allow for a compact syntax when calling functions.

```monobridge
void PrintA(inline number a):
    logInfo("a = " + a)

// Call with standard syntax
PrintA(10)

// Or call as inline
PrintA():
    10

// Call as inline with name
PrintA():
    a: 10
```

### Inline Functions

MonoBridge supports inline functions, providing additional flexibility in function calls.

```monobridge
void CallInlineFunction(inline Action action):
    action()

// Call with standard syntax
void InlineFunction():
    logInfo("Inline function")

CallInlineFunction(InlineFunction)

// Or call as inline
CallInlineFunction():
    logInfo("Inline function")

// Call as inline with named action
CallInlineFunction():
    action(): logInfo("Inline function")

// Call as inline with multiple actions
CallInlineFunction():
    action(): 
        logInfo("Inline function line 1")
        logInfo("Inline function line 2")
        logInfo("Inline function line 3")
```

### Cascading Functions

Cascading functions allow for chained calls and transformations.

```monobridge
cascading list<number> CascadingList():
    return list<number>[1,2,3,4,5]

void AddToCascadingList(cascading list<number> @list, number value):
    @list.Add(value)

// Call as standard function

var @list = CascadingList()

AddToCascadingList(@list, 6)

logInfo(@list) // [1,2,3,4,5,6]

// Or call as inline

list<number> result

CascadingList():
    AddToCascadingList(6)
    AddToCascadingList(7)

    result = cascading return

logInfo(result) // [1,2,3,4,5,6,7]
```

### Cascading Functions with Inline Functions

You can add inline functions to cascading functions to further control the chain.

```monobridge
cascading list<number> CascadingList(inline Func<number> getter):
    var @list = list<number>[1,2,3,4,5]
    @list.Add(getter())
    return @list

void AddToCascadingList(cascading list<number> @list, number value):
    @list.Add(value)

CascadingList():
    getter(): 6
    cascade:
        AddToCascadingList(7)
        AddToCascadingList(8)

        logInfo("Cascading function result is " + cascading return)
```

## Delegates

Delegates are a powerful feature in MonoBridge, allowing for the creation of function pointers and callbacks.

### Delegate Declaration

Delegates can be declared with a specific signature.

```monobridge

delegate void Callback()

delegate number NumberGetter()

delegate number BinaryOperation(number a, number b)

void CallCallback(Callback callback):
	callback()

void CallNumberGetter(NumberGetter getter):
	logInfo("Number is " + getter())

void CallBinaryOperation(BinaryOperation operation):
    logInfo("Result is " + operation(1, 2))

BinaryOperation add = (a, b) : a + b
BinaryOperation subtract = (a, b) : a - b
NumberGetter getTen = () : 10
Callback logHello = () : logInfo("Hello")

CallCallback(logHello)
CallNumberGetter(getTen)
CallBinaryOperation(add)
CallBinaryOperation(subtract)

// You can also use multiline syntax for delegates

BinaryOperation multiply = (a, b):
    logInfo("Multiplying " + a + " and " + b)
	return a * b

CallBinaryOperation(multiply)
```